#!/usr/bin/env perl
use Lingua::Shakespeare;

A rather infamous play by Shakespeare.

Romeo, a young man with a remarkable patience.
Hamlet, the signifier of this play (who had a relationship with Olivia).
Ophelia, a not very talkativ being.
Juliet, the voiceless but not so less important princess.
Othello, a reticent man, speaking only one word.
Olivia, a young man capable of combining Ophelia's and Othello's speaking abilities.


                    
		Act I: Hamlet's praising.

                    Scene I: The introduction of Hamlet.

	[Enter Hamlet and Romeo]
	Romeo: Listen to your heart!
	[Exit Romeo]


 		Scene II: The introduction of Ophelia.
 		
	[Enter Ophelia]
	Hamlet: You pretty little warm thing! Thou art as prompt as the sum of
	the square of thyself and your amazing golden hair. 
	Thou are as rich as the sum of thyself and an amazing lord. 
	[Exit Ophelia]


		Scene III: The introduction of Juliet.
		
	[Enter Juliet]
	Hamlet: You are an amazing warm fine summer's day. You are as good as the sum of yourself and a lovely angel. 
	Speak your mind!
	[Exit Juliet]


		Scene IV: The introduction of Olivia.

	[Enter Olivia]
	Hamlet: You lying cowardly dirty disgusting devil. Thou art as good as the difference between 
	a warm peaceful sunny blossoming summer's day and thyself. Thou art as bad as the sum of thyself and an animal.
	Thou art as bad as the product of thyself and a big horse.
	[Exit Olivia]


		Scene V: The praising of Othello.

	[Enter Othello]
	Hamlet: You brave noble mighty gentle trustworthy hero! Thou art as good as the difference between the sum 
	of thyself and a rich big brave white proud lord and a rotten oozing large hound. You are as brave as the sum of 
	yourself and a large famine. 
	[Exit Othello]
	[Enter Romeo]
		
		
		
			Act II: It begins.
		Scene I: The neverending story introduction.

	Romeo: Am I as good as you?
	Hamlet: You are as good as the sum of yourself and a summer's day!
	Hamlet: If so, let us proceed to Act III. 
	Hamlet: If not, let us proceed to Scene II.
		

			Scene II: The harsh truth.

	Romeo: Is the remainder of the quotient between me and the sum of an amazing fine blossoming warm summer's day 
		and a codpiece as good as nothing?
	Hamlet: If so, we shall proceed to Scene III!
	Hamlet: If not, let us proceed to Scene IV!


			Scene III: Othello's appearance.
	[Exit Romeo]
	[Enter Othello]
	Hamlet: Speak your mind!
	[Exit Othello]
	[Enter Olivia]
	Hamlet: Speak thy mind!
	[Exit Olivia]
	Hamlet: Let us proceed to Scene VII.


			Scene IV: Ophelia's appearance.

	Romeo: Is the remainder of the quotient between me and the sum of a rich brave Lord and a hound
	as good as nothing?
	Hamlet: If not, we shall proceed to Scene V.
	[Exit Romeo]
	[Enter Ophelia]
	Hamlet: Speak thy mind!
	[Exit Ophelia]
	Hamlet: We shall proceed to Scene VII.


			Scene V: Olivia's appearance.
			
	Romeo: Is the remainder of the quotient between me and the sum of the amazing blue heaven
		and an animal as good as nothing?
	Hamlet: If not, we shall return to Scene VI!
	[Exit Romeo]
	[Enter Olivia]
	Hamlet: Speak thy mind!
	[Exit Olivia]
	Hamlet: We shall Proceed to Scene VII.


			Scene VI: The everyday life.
		
	Hamlet: Open thy heart!
	[Exit Romeo]
	Hamlet: We shall proceed to Scene VII.


			Scene VII: Juliet's appearance.
		
	[Enter Juliet]
	Hamlet: Speak thy mind!
	[Exit Juliet]
	[Enter Romeo]
	Hamlet: Let us return to Scene I!

			Act III: The end.
			Scene I: Romeo dies.
		
[Exeunt]



